OUTPUT_FORMAT("elf32-i386", "elf32-i386", "elf32-i386")
OUTPUT_ARCH(i386)
ENTRY(_start32)
SECTIONS
{
	. = 1m;
	_begin = . ;
	.text :
	AT (ADDR(.text) & 0x0fffffff)
	{
		*(.multiboot)
		*(.text)
		*(.text.*)
		*(.stub)
		*(.note*)
	}
	_etext = . ;

	.rodata :
	AT (LOADADDR(.text) + (ADDR(.rodata) - ADDR(.text)))
	{
		*(.rodata)
		__init_array_start = . ;
		*(SORT(.init_array.*))
		__init_array_end = . ;
		__fini_array_start = . ;
		*(SORT(.fini_array.*))
		__fini_array_end = . ;
	}

	_data_start = .;
	.data :
	AT (LOADADDR(.text) + (ADDR(.data) - ADDR(.text)))
	{
		*(.data)
	}
	_edata = . ;

	.tdata :
	AT (LOADADDR(.text) + (ADDR(.tdata) - ADDR(.text)))
	{
		_tdata_start = . ;
		*(.tdata)
		_tdata_end = . ;
	}

	.tbss :
	AT (LOADADDR(.text) + (ADDR(.tbss) - ADDR(.text)))
	{
		_tbss_start = . ;
		*(.tbss)
		_tbss_end = . ;
	}

	__bss_start = . ;
	.bss :
	AT (LOADADDR(.text) + (ADDR(.bss) - ADDR(.text)))
	{
		*(.bss)
		*(COMMON)
		*(.bootstack)
	}
	_end = . ;
	PROVIDE (end = .) ;
}
